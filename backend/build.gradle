plugins {
    id 'java'
    id 'org.springframework.boot' version '3.4.1'
    id 'io.spring.dependency-management' version '1.1.7'
    id 'jacoco'
}

group = 'com.cartshare'
version = '0.0.1-SNAPSHOT'
description = 'backend'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

repositories {
    mavenCentral()
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

dependencies {
    implementation platform('org.springframework.boot:spring-boot-dependencies:3.4.1')

    // Web and Security
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-security'

    // Cloud Firestore
    implementation 'com.google.cloud:spring-cloud-gcp-starter-data-firestore'
    implementation 'com.google.cloud:spring-cloud-gcp-starter'
    implementation 'org.springframework.boot:spring-boot-starter-webflux'

    // Swagger/OpenAPI
    implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.8.3'

    // Utilities
    implementation 'org.jspecify:jspecify:1.0.0'
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'
    developmentOnly 'org.springframework.boot:spring-boot-devtools'

    // Validation & Excel
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.apache.poi:poi:5.4.0'
    implementation 'org.apache.poi:poi-ooxml:5.4.0'
    implementation 'org.apache.logging.log4j:log4j-api:2.17.2'

    // Testing
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.security:spring-security-test'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
    testImplementation 'org.apache.poi:poi:5.4.0'
    testImplementation 'org.apache.poi:poi-ooxml:5.4.0'
}

dependencyManagement {
    imports {
        mavenBom "com.google.cloud:spring-cloud-gcp-dependencies:5.9.0"
    }
}

// --- JaCoCo Configuration ---

jacoco {
    toolVersion = "0.8.11"
}

tasks.named('test') {
    useJUnitPlatform()
    exclude '**/FirestoreDataSeeder.class'

    // Optional: Log to console so you know it's being skipped
    testLogging {
        events "passed", "skipped", "failed"
    }
    // Resolves Mockito/JDK 21 agent warnings
    jvmArgs("-XX:+EnableDynamicAgentLoading")
    finalizedBy jacocoTestReport
}

jacocoTestReport {
    dependsOn test
    reports {
        xml.required = true
        html.required = true
    }

    // Excludes classes from the HTML visual report
    afterEvaluate {
        classDirectories.setFrom(files(classDirectories.files.collect {
            fileTree(dir: it, exclude: [
                    "**/com/cartshare/backend/core/model/**",
                    "**/com/cartshare/backend/config/**",
                    "**/*Application*",
                    "**/*Builder*"
            ])
        }))
    }
}

jacocoTestCoverageVerification {
    dependsOn jacocoTestReport
    violationRules {
        rule {
            element = 'CLASS'
            limit {
                counter = 'LINE'
                value = 'COVEREDRATIO'
                minimum = 0.80 // 80% Coverage Requirement
            }
            // Excludes classes from the verification math
            excludes = [
                    "com.cartshare.backend.core.model.*",
                    "com.cartshare.backend.config.*",
                    "com.cartshare.backend.BackendApplication",
                    "com.cartshare.backend.shared.util.FirestoreDataSeeder"
            ]
        }
    }
}

// Force verification to run during build
check.dependsOn jacocoTestCoverageVerification